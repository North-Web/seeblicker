prototype(NorthWeb.Site:Content.Swiper.Item) < prototype(Neos.Neos:ContentComponent) {
    text = Neos.Neos:Editable {
        property = 'text'
    }
    headline= ${q(node).property('headline')}
    headlineShow = ${q(node).property('headline') ? true : false}
    textShow = ${q(node).property('text') || site.context.inBackend ? true : false}

    src = Neos.Fusion:Case {
        image {
            condition = ${q(node).property('image') ? true : false}
            renderer = Sitegeist.Kaleidoscope:AssetImageSource {
                asset = ${q(node).property('image')}
                width = 600
            }
        }
        dummyImage {
            condition = ${site.context.inBackend}
            renderer = Sitegeist.Kaleidoscope:DummyImageSource {
                width = 600
                height = 400
            }
        }
    }

    styleClass = Neos.Fusion:Join {
        1 = ${'swiperText'}
        1.@if.BE = ${site.context.inBackend ? false : true}
        2 = ${'p-4'}
        3 = ${'w-100'}
        4 = ${q(node).property('styleBackgroundContainer')}
        @glue = ' '
    }

    renderer = afx`
        <div class="swiper-slide flex-column">
            <Sitegeist.Lazybones:Image
                    lazy={false}
                    imageSource={props.src}
                    alt={props.alternativeText}/>
            <div @if.hasHeadline={props.headlineShow} class="swiperHeadline align-self-center">{props.headline}</div>
            <div @if.hasText={props.textShow} class={props.styleClass}>{props.text}</div>
        </div>
    `
}
