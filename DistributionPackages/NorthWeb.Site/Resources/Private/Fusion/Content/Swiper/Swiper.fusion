prototype(NorthWeb.Site:Content.Swiper) < prototype(Neos.Neos:ContentComponent) {
    styleClass = Neos.Fusion:Join {
        1 = ${'contentSwiper'}
        2 = ${q(node).property('paddingTop')}
        3 = ${q(node).property('paddingBottom')}
        4 = ${q(node).property('styleBackgroundContainer')}
        @glue = ' '
    }

    title = Neos.Neos:Editable {
        property = 'title'
    }

    text = Neos.Neos:Editable {
        property = 'text'
    }
    items = Neos.Neos:ContentCollection {
        nodePath = 'items'
        tagName = 'div'
        attributes {
            class = 'swiper-wrapper align-items-center'
        }
    }

    styleClassTextButton = Neos.Fusion:Join {
        1 = ${'actionButton p-4 '}
        2 = ${q(node).property('actionButtonStyle')}
        @glue = ' '
    }

    styleClassTextButtonMobile = Neos.Fusion:Join {
        1 = ${'text-center w-100 actionButtonMobil p-3'}
        2 = ${q(node).property('actionButtonStyle')}
        @glue = ' '
    }

    actionButtonText = ${q(node).property('actionButtonText')}

    renderer = afx`
        <div>
            <div class="container swiperBadge">
                <div class="actionButtonWrap d-none d-lg-block">
                    <div class={props.styleClassTextButton} >
                        <span>{props.actionButtonText}</span>
                    </div>
                </div>
            </div>
            <div class="d-lg-none">
                <div class={props.styleClassTextButtonMobile}>
                    <h3>{props.actionButtonText}</h3>
                </div>
            </div>
            <div class={props.styleClass}>
                <div class="container">
                    <div class="text text-center pb-5">
                        <div>
                            {props.title}
                        </div>
                        <div>{props.text}</div>
                    </div>
                    <div class="swiper-container">
                        {props.items}
                        <div class="swiper-button-next"></div>
                        <div class="swiper-button-prev"></div>
                    </div>
                </div>
            </div>
        </div>
    `
}

prototype(NorthWeb.Site:IncludeCase) < prototype(Neos.Fusion:Value) {
    value = Carbon.IncludeAssets:Case {
        contentCollection = ${'[instanceof Neos.Neos:ContentCollection]'}
        content = ${'NorthWeb.Site:Content.Swiper'}
        }
}
